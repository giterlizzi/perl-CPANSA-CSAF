#!perl

use v5.10;
use strict;
use warnings;

use Carp;
use Getopt::Long qw( GetOptions :config gnu_compat );

use Mojo::File qw(curfile);
use Mojo::Template;
use Mojo::Collection;

use CSAF::Parser;

my %options = ();

GetOptions(
    \%options, qw(
        csaf-index-file|f=s
        output-directory|o=s
    )
) or Carp::croak 'Specify an option';

Carp::croak "Missing CSAF index.txt file" unless $options{'csaf-index-file'};
Carp::croak "Missing output directory"    unless $options{'output-directory'};

my $csaf_index_file = Mojo::File->new($options{'csaf-index-file'});
my $csaf_base_dir   = $csaf_index_file->dirname;

my $index_file = Mojo::File->new($options{'output-directory'})->path('index.html');

my @csaf  = split /\n/, $csaf_index_file->slurp;
my @items = ();

foreach (@csaf) {

    my $csaf_file = Mojo::File->new($csaf_base_dir)->child($_);

    say STDERR "[*] Process $csaf_file";

    my $csaf = eval { CSAF::Parser->new(file => $csaf_file)->parse };

    if ($@) {
        say STDERR "    ERROR - $@";
        next;
    }

    my $csaf_id        = $csaf->document->tracking->id;
    my $csaf_url       = undef;
    my $csaf_html_path = ($csaf_file->basename('.json') . '.html');
    my $current_date   = $csaf->document->tracking->current_release_date;
    my $severity       = $csaf->document->aggregate_severity->text;
    my @cves           = ();

    foreach my $vulnerability ($csaf->vulnerabilities->each) {
        push @cves, $vulnerability->cve;
    }

    foreach my $reference ($csaf->document->references->each) {
        $csaf_url = $reference->url if $reference->category eq 'self';
        last                        if $csaf_url;
    }

    my $item = {
        csaf_id        => $csaf_id,
        current_date   => $current_date,
        severity       => $severity,
        csaf_html_path => $csaf_html_path,
        csaf_url       => $csaf_url,
        cves           => Mojo::Collection->new(@cves)
    };

    push @items, $item;

    say STDERR "    Render CSAF document in HTML";

    my $csaf_html_content = eval { $csaf->render(format => 'html') };

    if ($@) {
        say STDERR "    ERROR - $@";
        next;
    }

    my $csaf_html_file = Mojo::File->new($options{'output-directory'})->child($csaf_html_path);

    say STDERR "    Save in $csaf_html_file";

    $csaf_html_file->spew($csaf_html_content);
    last;

}

my $mt = Mojo::Template->new(vars => 1);

$index_file->spew(
    $mt->render(
        <<'EOF', {items => Mojo::Collection->new(@items)->sort(sub { $b->{current_date}->datetime cmp $a->{current_date}->datetime })}));
<!DOCTYPE html>
<html lang="en">
<head>
  <meta name="viewport" content="width=device-width, initial-scale=1">
  <link rel="stylesheet" href="https://unpkg.com/gutenberg-css" charset="utf-8">
  <link rel="stylesheet" href="https://unpkg.com/gutenberg-css/dist/themes/modern.min.css" charset="utf-8">
  <meta charset="utf-8" />
  <title>CPAN Security Advisories</title>
  <style>
    .severity {
        color: #fff;
        font-weight: bold;
        padding-left: .20em;
        padding-right: .20em; 
        display: inline-block;
        border-radius: 5px;
    }
    .severity-critical { background-color: #343a40 !important; }
    .severity-high { background-color: #dc3545 !important; }
    .severity-medium { background-color: #fd7e14 !important; }
    .severity-low { background-color: #ffc107 !important; }
  </style>
</head>
<body>
  <h1>CPAN Security Advisories</h1>
  <table class="table">
    <thead>
      <tr>
        <th># CPANSA ID</th>
        <th>Severity</th>
        <th>Date</th>
        <th>CVEs</th>
        <th>&nbsp;</th>
      </tr>
    </thead>
    <tbody>
    % foreach my $item ($items->each) {
      <tr>
        <th>
            <a href="<%= $item->{csaf_html_path} %>"><%= $item->{csaf_id} %></a>
        </th>
        <td>
        % if ($item->{severity}) {
            <span class="severity severity-<%= lc $item->{severity} %>"><%= ucfirst $item->{severity} %></span>
        % }
        </td>
        <td>
            <%= $item->{current_date}->date %>
        </td>
        <td>
        % foreach my $cve ($item->{cves}->each) {
            <a href="https://nvd.nist.gov/vuln/detail/<%= $cve %>"><%= $cve %></a>
        % }
        </td>
        <td>
            <a href="<%= $item->{csaf_url} %>">CSAF</a>
        </td>
      </tr>
    % }
    </tbody>
  </table>
</body>
</html>
EOF
